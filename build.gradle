allprojects {
    apply plugin: 'java'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation 'org.jetbrains:annotations:26.0.2'
        implementation 'com.google.code.gson:gson:2.10.1'
        implementation 'com.google.guava:guava:33.4.8-jre'
        compileOnly 'org.projectlombok:lombok:1.18.38'
        annotationProcessor 'org.projectlombok:lombok:1.18.38'
    }
}

project('acs-client:java') { proj ->
    apply plugin: 'maven-publish'

    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId = 'com.github.ZorTik'
                artifactId = 'acs-client'
                version = proj.version

                from components.java
                pom {
                    name = 'ACS Client'
                    description = 'A client library for interacting with the ACS (Access Control System) API.'
                    developer {
                        id = 'zort'
                        name = 'ZorTik'
                        email = 'zortlegit@gmail.com'
                    }
                }
            }
        }
    }
}

['acs-server', 'acs-plane'].each {
    project(it).dependencies {
        implementation project('acs-proto')
    }
}

['acs-server', 'acs-spring-boot-starter', 'acs-plane', 'acs-proto'].each {
    project(it).dependencies {
        implementation project('acs-core')
    }
}

['acs-core', 'acs-spring-boot-starter'].each {
    project(it).dependencies {
        compileOnly project('acs-client:java')
    }
}

['examples:spring-user-system-relation'].each {
    project(it).dependencies {
        implementation project('acs-client:java')
        implementation project('acs-spring-boot-starter')
    }
}